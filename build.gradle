buildscript {
    repositories {
        mavenCentral()

        maven {
            url "https://plugins.gradle.org/m2/"
        }

        maven {
            url "https://dl.bintray.com/hhu-bsinfo/dxram"
        }

        maven {
            url 'https://oss.jfrog.org/artifactory/oss-snapshot-local'
        }
    }

    dependencies {
        classpath "de.hhu.bsinfo:dxbuild:0.4.0" + (Boolean.valueOf(System.getProperty('release')) ? '' : '-SNAPSHOT')
    }
}

plugins {
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id "com.jfrog.artifactory" version "4.7.5"
}

apply plugin: 'dxbuild'
group 'de.hhu.bsinfo'
version '0.8.0' + (Boolean.valueOf(System.getProperty('release')) ? '' : '-SNAPSHOT')

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

mainClassName = 'de.hhu.bsinfo.dxmem.DXMemMain'

applicationDefaultJvmArgs = [
        '-Dlog4j.configurationFile=file:DXMEM_HOME/config/log4j2.xml'
]

project.startScripts.with {
    doLast {
        unixScript.text = unixScript.text.replace('DXMEM_HOME', '\$APP_HOME')
    }
}

repositories {
    mavenCentral()

    maven {
        url "https://dl.bintray.com/hhu-bsinfo/dxram"
    }

    maven {
        url 'https://oss.jfrog.org/artifactory/oss-snapshot-local'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Werror", "-Xlint:sunapi", "-XDenableSunApiLintControl"]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

dependencies {
    implementation fileTree(dir: 'lib', include: ['*.jar'])

    implementation "de.hhu.bsinfo:dxmon:0.5.0" + (Boolean.valueOf(System.getProperty('release')) ? '' : '-SNAPSHOT')
    implementation "de.hhu.bsinfo:dxutils:0.8.0" + (Boolean.valueOf(System.getProperty('release')) ? '' : '-SNAPSHOT')

    implementation 'org.jline:jline:3.9.0'
    implementation 'info.picocli:picocli:3.5.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.9.0'

    extTestImplementation 'junit:junit:4.12'
    extTestImplementation 'org.assertj:assertj-core:3.9.0'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

apply from: 'publish.gradle'
apply from: 'bintray.gradle'
apply from: 'artifactory.gradle'
apply from: 'types.gradle'

task wrapper(type: Wrapper) {
    gradleVersion = "4.10"
}
